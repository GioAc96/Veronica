plugins {
    id 'java'
    id "io.freefair.lombok" version "4.1.6"
    id 'jacoco'
    id 'checkstyle'
    id("maven-publish")
}

group 'rocks.gioac96'
version '0.2.1-preview'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jacocoTestReport {

    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }

}

dependencies {

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.0'
    testCompile group: 'org.mockito', name: 'mockito-inline', version: '3.2.4'

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    toolVersion "8.25"
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    ignoreFailures = false
    maxWarnings = 0
    checkstyleTest.enabled = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/GioAc96/Veronica")
            credentials {
                username = "GioAc96"
                password = new File('GH_TOKEN').getText()
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}